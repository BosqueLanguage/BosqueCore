//The core namespace implements the primitive types used in the Bosque language
namespace Core;

//<summary>Special type which all values are instances of.</summary>
__universal concept Any {
}

//<summary>Special type for representing all non-none values.</summary>
__universal concept Some provides Any {
}

//<summary>Special type for indicating a value can be used as a key in a collection.</summary>
__universal concept KeyType provides Any {
    function equal<T KeyType>(a: T, b: T): Bool = special_method;
    function less<T KeyType>(a: T, b: T): Bool = special_method;
}

//<summary>Type for indicating that a datatype is a Validator.</summary>
__universal concept RegexValidator provides Any {
}

//<summary>Type for indicating that a datatype is a Validator.</summary>
__universal concept ASCIIRegexValidator provides Any {
}

//<summary>Type for indicating that a datatype is a Validator.</summary>
__universal concept PathValidator provides Any {
}

//<summary>Type for representing the none value.</summary>
__internal entity None provides KeyType {
}

//<summary>Primitive boolean value.</summary>
__internal __typedeclable __typebase entity Bool provides Some, KeyType {
}

//<summary>Primitive integer value.</summary>
__internal __typedeclable __typebase __numeric entity Int provides Some, KeyType {
    method toNat(): Nat { assert(this >= 0i); return NumericOps::s_intToNat(this); }
    method toBigNat(): BigNat { assert(this >= 0i); return NumericOps::s_bigIntToBigNat(NumericOps::s_intToBigInt(this)); }
    method toBigInt(): BigInt { return NumericOps::s_intToBigInt(this); }

    function min(a: Int, b: Int): Int {if(a < b) { return a; } else { return b; }}
    function max(a: Int, b: Int): Int {if(a > b) { return a; } else { return b; }}
}

//<summary>Primitive natural number value.</summary>
__internal __typedeclable __typebase __numeric entity Nat provides Some, KeyType {
    method toBigNat(): BigNat { return NumericOps::s_natToBigNat(this); }
    method toInt(): Int { return NumericOps::s_natToInt(this); }
    method toBigInt(): BigInt { return NumericOps::s_bigNatToBigInt(NumericOps::s_natToBigNat(this)); }

    function min(a: Nat, b: Nat): Nat {if(a < b) { return a; } else { return b; }}
    function max(a: Nat, b: Nat): Nat {if(a > b) { return a; } else { return b; }}
}

//<summary>Primitive big-integer value.</summary>
__internal __typedeclable __typebase __numeric entity BigInt provides Some, KeyType {
    function min(a: BigInt, b: BigInt): BigInt {if(a < b) { return a; } else { return b; }}
    function max(a: BigInt, b: BigInt): BigInt {if(a > b) { return a; } else { return b; }}
}

//<summary>Primitive big-integer value.</summary>
__internal __typedeclable __typebase __numeric entity BigNat provides Some, KeyType {
    function min(a: BigNat, b: BigNat): BigNat {if(a < b) { return a; } else { return b; }}
    function max(a: BigNat, b: BigNat): BigNat {if(a > b) { return a; } else { return b; }}
}

//<summary>Primitive float value defaults to 64bit.</summary>
__internal __typedeclable __typebase __numeric entity Float provides Some {
    const pi: Float = 3.1415926535897931f;
    const e: Float = 2.7182818284590451f;

    function min(a: Float, b: Float): Float {if(a < b) { return a; } else { return b; }}
    function max(a: Float, b: Float): Float {if(a > b) { return a; } else { return b; }}

    method floor(): Float { return NumericOps::s_floatFloor(this); }
    method ceiling(): Float { return NumericOps::s_floatCeil(this); }
    method truncate(): Float { return NumericOps::s_floatTruncate(this); }

    method pow(x: Float): Float {
        return NumericOps::s_floatPower(this, x);
    }

    method square(): Float {
        return this * this;
    }

    method cube(): Float {
        return this * this * this;
    }

    method sqrt(): Float
        requires this >= 0.0f;
    {
        return NumericOps::s_floatSqrt(this);
    }
}

//<summary>Primitive float decimal value.</summary>
__internal __typedeclable __typebase __numeric entity Decimal provides Some {
    const pi: Decimal = 3.1415926535897931d;
    const e: Decimal = 2.7182818284590451d;

    function min(a: Decimal, b: Decimal): Decimal {if(a < b) { return a; } else { return b; }}
    function max(a: Decimal, b: Decimal): Decimal {if(a > b) { return a; } else { return b; }}

    method floor(): Decimal { return NumericOps::s_decimalFloor(this); }
    method ceiling(): Decimal { return NumericOps::s_decimalCeil(this); }
    method truncate(): Decimal { return NumericOps::s_decimalTruncate(this); }

    method pow(x: Decimal): Decimal {
        return NumericOps::s_decimalPower(this, x);
    }

    method square(): Decimal {
        return this * this;
    }

    method cube(): Decimal {
        return this * this * this;
    }

    method sqrt(): Decimal
        requires this >= 0.0d;
    {
        return NumericOps::s_decimalSqrt(this);
    }
}

//<summary>Primitive rational value.</summary>
__internal __typedeclable __typebase __numeric entity Rational provides Some {
    function min(a: Rational, b: Rational): Rational {if(a < b) { return a; } else { return b; }}
    function max(a: Rational, b: Rational): Rational {if(a > b) { return a; } else { return b; }}
}

//<summary>Primitive rational value.</summary>
__internal __typedeclable __typebase __numeric entity DecimalDegree provides Some {
}

//<summary>Primitive rational value.</summary>
__internal __typedeclable __typebase entity LatLongCoordinate provides Some {
}

//<summary>Primitive rational value.</summary>
__internal __typedeclable __typebase __numeric entity Complex provides Some {
}

//<summary>Primitive string value.</summary>
__internal __typedeclable __typebase entity String provides Some, KeyType {
    function concat(sl: List<String>): String {
        return StringOps::s_strconcat(sl);
    }

    function concatOf(...sl: List<String>): String {
        return StringOps::s_strconcat(sl);
    }

    function join(sep: String, sl: List<String>): String {
        return StringOps::s_strjoin(sl, sep);
    }

    function joinOf(sep: String, ...sl: List<String>): String {
        return StringOps::s_strjoin(sl, sep);
    }

    method empty(): Bool {
        return StringOps::s_empty(this);
    }

    method append(s: String): String {
        return StringOps::s_concat2(this, s);
    }

    method prepend(s: String): String {
        return StringOps::s_concat2(s, this);
    }

    //
    //TODO: add regex support to these
    //

    method startsWith(s: String): Bool {
        return StringOps::s_startsWith(this, s);
    }

    method extractFront(s: String): String {
        return StringOps::s_extractFront(this, s);
    }

    method removeFront(s: String): String {
        return StringOps::s_removeFront(this, s);
    }

    method endsWith(s: String): Bool {
        return StringOps::s_endsWith(this, s);
    }

    method extractEnd(s: String): String {
        return StringOps::s_extractEnd(this, s);
    }

    method removeEnd(s: String): String {
        return StringOps::s_removeEnd(this, s);
    }

    method contains(s: String): Bool {
        return StringOps::s_contains(this, s);
    }

    method replaceUnique(str: String, withstr: String): String 
        requires this.contains(str);
    {
        return StringOps::s_replaceUnique(this, str, withstr);
    }

    method replaceUniqueTry(str: String, withstr: String): String {
        return StringOps::s_replaceUnique(this, str, withstr);
    }

    method replaceAll(str: String, withstr: String): String
        requires this.contains(str);
    {
        return StringOps::s_replaceAll(this, str, withstr);
    }

    method replaceAllTry(str: String, withstr: String): String {
        return StringOps::s_replaceAll(this, str, withstr);
    }

    function fromNat(n: Nat): String {
        return StringOps::s_natToStr(n);
    }

    /*
    //TODO: other fromX operations

    //TODO: other toX operations

    method startsWith(v: String | Regex): Bool {
        use RE(.*)
    }

    method endsWith(v: String | Regex): Bool {
        use (.*)RE
    }

    method includes(v: String | Regex): Bool {
        use (.*)RE(.*)
    }

    method extractFront(v: String | Regex): String {
        use search on RE(.*) -- take maximal slice
    }

    method extractEnd(v: String | Regex): String {
        use search on (.*)RE -- take maximal slice
    }

    method removeFront(v: String | Regex): String {
        use search on RE(.*) -- take maximal slice
    }

    method removeEnd(v: String | Regex): String {
        use search on (.*)RE -- take maximal slice
    }

    method splitAtFirst(v: String | Regex): String, String, String {
        use search on min position of RE(.*)
    }

    method splitAll(...seps: List<String>): List<String> {
        split on every sep provided
    }
    */
}

//<summary>Primitive safe string value.</summary>
__internal __constructable __typedeclable entity StringOf<T: RegexValidator> provides Some, KeyType {
    private function s_into(s: String): StringOf<T> = special_inject;
    method value(): String = special_extract;

    function coerce(s: String): StringOf<T> {
        assert T::accepts(s);
        return StringOf<T>::s_into(s);
    } 

    function tryCoerce(s: String): StringOf<T>? {
        if(!T::accepts(s)) {
            return none;
        } 
        else {
            return StringOf<T>::s_into(s);
        }
    }
}

//<summary>Primitive string value.</summary>
__internal __typedeclable __typebase entity ASCIIString provides Some, KeyType {
    __safe private function s_empty(s: ASCIIString): Bool = s_ascii_string_empty;
    __assume_safe private function s_concat2(s1: ASCIIString, s2: ASCIIString): ASCIIString = s_ascii_string_append;

    function concat(sl: List<ASCIIString>): ASCIIString {
        return ASCIIStringOps::s_strconcat(sl);
    }

    function concatOf(...sl: List<ASCIIString>): ASCIIString {
        return ASCIIStringOps::s_strconcat(sl);
    }

    function join(sep: ASCIIString, sl: List<ASCIIString>): ASCIIString {
        return ASCIIStringOps::s_strjoin(sl, sep);
    }

    function joinOf(sep: ASCIIString, ...sl: List<ASCIIString>): ASCIIString {
        return ASCIIStringOps::s_strjoin(sl, sep);
    }

    method empty(): Bool {
        return ASCIIString::s_empty(this);
    }

    method append(s: ASCIIString): ASCIIString {
        return ASCIIString::s_concat2(this, s);
    }

    method prepend(s: ASCIIString): ASCIIString {
        return ASCIIString::s_concat2(s, this);
    }

    //
    //TODO: add regex support to these
    //

    method startsWith(s: ASCIIString): Bool {
        return ASCIIStringOps::s_startsWith(this, s);
    }

    method extractFront(s: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_extractFront(this, s);
    }

    method removeFront(s: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_removeFront(this, s);
    }

    method endsWith(s: ASCIIString): Bool {
        return ASCIIStringOps::s_endsWith(this, s);
    }

    method extractEnd(s: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_extractEnd(this, s);
    }

    method removeEnd(s: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_removeEnd(this, s);
    }

    method contains(s: ASCIIString): Bool {
        return ASCIIStringOps::s_contains(this, s);
    }

    method replaceUnique(str: ASCIIString, withstr: ASCIIString): ASCIIString 
        requires this.contains(str);
    {
        return ASCIIStringOps::s_replaceUnique(this, str, withstr);
    }

    method replaceUniqueTry(str: ASCIIString, withstr: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_replaceUnique(this, str, withstr);
    }

    method replaceAll(str: ASCIIString, withstr: ASCIIString): ASCIIString
        requires this.contains(str);
    {
        return ASCIIStringOps::s_replaceAll(this, str, withstr);
    }

    method replaceAllTry(str: ASCIIString, withstr: ASCIIString): ASCIIString {
        return ASCIIStringOps::s_replaceAll(this, str, withstr);
    }

    function fromNat(n: Nat): ASCIIString {
        return ASCIIStringOps::s_natToStr(n);
    }
}

//<summary>Primitive safe string value.</summary>
__internal __constructable __typedeclable entity ASCIIStringOf<T: ASCIIRegexValidator> provides Some, KeyType {
    private function s_into(s: ASCIIString): ASCIIStringOf<T> = special_inject;
    method value(): ASCIIString = special_extract;

    function coerce(s: ASCIIString): ASCIIStringOf<T> {
        assert T::accepts(s);
        return ASCIIStringOf<T>::s_into(s);
    } 

    function tryCoerce(s: ASCIIString): ASCIIStringOf<T>? {
        if(!T::accepts(s)) {
            return none;
        } 
        else {
            return ASCIIStringOf<T>::s_into(s);
        }
    }
}

//<summary>Special declaration of string interpolation function.</summary>
function interpolate(): String = special_method;

//<summary>An efficient byte buffer for data.</summary>
__internal __constructable entity StringView provides Some {
    //TODO: do we want to make this typedeclable too?
}

//<summary>An efficient byte buffer for data.</summary>
__internal __constructable entity ASCIIStringView provides Some {
    //TODO: do we want to make this typedeclable too?
}

//<summary>An efficient byte buffer for data.</summary>
__internal entity ByteBuffer provides Some {
    //TODO: do we want to make this typedeclable too?
}

//<summary>Validator for TimeZone</summary>
validator TimeZoneRegex = /"{"[A-Za-z_ 0-9-]+"}"/;
typedecl TimeZoneString = ASCIIStringOf<TimeZoneRegex>;

//<summary>Primitive DateTime value.</summary>
__internal __typedeclable __typebase entity DateTime provides Some {
    __assume_safe internal function s_create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat, tzdata: TimeZoneString): DateTime = datetime_create;

    function create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat, tzdata: TimeZoneString): DateTime
        requires /\(1900n <= year, year <= 2200n);
        requires month < 12n;
        requires DateOps::s_check_DayInMonth(day, month, year);
        requires hour < 24n;
        requires min < 60n;
        requires sec < 61n;
    {
        return DateTime::s_create(year, month, day, hour, min, sec, tzdata);
    }
}

__internal __typedeclable __typebase entity UTCDateTime provides Some, KeyType {
    __assume_safe internal function s_create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat): UTCDateTime = utcdatetime_create;

    function create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat): UTCDateTime
        requires /\(1900n <= year, year <= 2200n);
        requires month < 12n;
        requires DateOps::s_check_DayInMonth(day, month, year);
        requires hour < 24n;
        requires min < 60n;
        requires sec < 61n;
    {
        return UTCDateTime::s_create(year, month, day, hour, min, sec);
    }
}

__internal __typedeclable __typebase entity PlainDate provides Some, KeyType {
    __assume_safe internal function s_create(year: Nat, month: Nat, day: Nat): PlainDate = plaindate_create;

    function create(year: Nat, month: Nat, day: Nat): PlainDate
        requires /\(1900n <= year, year <= 2200n);
        requires month < 12n;
        requires DateOps::s_check_DayInMonth(day, month, year);
    {
        return PlainDate::s_create(year, month, day);
    }
}

__internal __typedeclable __typebase entity PlainTime provides Some, KeyType {
    __assume_safe internal function s_create(hour: Nat, min: Nat, sec: Nat): PlainTime = plaintime_create;

    function create(hour: Nat, min: Nat, sec: Nat): PlainTime
        requires hour < 24n;
        requires min < 60n;
        requires sec < 61n;
    {
        return PlainTime::s_create(hour, min, sec);
    }
}

//<summary>Primitive TickTime value (TIA time).</summary>
__internal __typedeclable __typebase entity TickTime provides Some, KeyType {
}

//<summary>Primitive LogicalTime value.</summary>
__internal __typedeclable __typebase entity LogicalTime provides Some, KeyType {
    __safe internal function s_zero(): LogicalTime = logicaltime_zero;
    __safe internal function s_increment(t: LogicalTime): LogicalTime = logicaltime_increment;
    __safe internal function s_refincrement(ref t: LogicalTime): void = logicaltime_increment;

    const zero: LogicalTime = LogicalTime::s_zero();

    method increment(): LogicalTime {
        return LogicalTime::s_increment(this);
    }

    ref method increment(): void {
        return LogicalTime::s_refincrement(ref this);
    }
}

__internal __typedeclable __typebase entity ISOTimeStamp provides Some, KeyType {
    __assume_safe internal function s_create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat, millis: Nat): ISOTimeStamp = isotimestamp_create;

    function create(year: Nat, month: Nat, day: Nat, hour: Nat, min: Nat, sec: Nat, millis: Nat): ISOTimeStamp
        requires /\(1900n <= year, year <= 2200n);
        requires month < 12n;
        requires DateOps::s_check_DayInMonth(day, month, year);
        requires hour < 24n;
        requires min < 60n;
        requires sec < 61n;
        requires millis < 1000n;
    {
        return ISOTimeStamp::s_create(year, month, day, hour, min, sec, millis);
    }
}

//<summary>Primitive UUIDv4 value.</summary>
__internal __typedeclable __typebase entity UUIDv4 provides Some, KeyType {
}

//<summary>Primitive UUIDv7 value.</summary>
__internal __typedeclable __typebase entity UUIDv7 provides Some, KeyType {
}

//<summary>Primitive hash value suitable for content addressable operations or crypto secure digests.</summary>
__internal __typedeclable __typebase entity SHAContentHash provides Some, KeyType {
}

//
//TODO: all of the deltas here
//

//<summary>Primitive unicode regex value.</summary>
__internal entity UnicodeRegex provides Some {
    __safe internal function s_accepts(re: UnicodeRegex, s: String): Bool = regex_accepts;

    method accepts(s: String): Bool {
        return UnicodeRegex::s_accepts(this, s);
    }
}

//<summary>Primitive unicode regex value.</summary>
__internal entity ASCIIRegex provides Some {
    __safe internal function s_accepts(re: ASCIIRegex, s: ASCIIString): Bool = regex_accepts;

    method accepts(s: ASCIIString): Bool {
        return Regex::s_accepts(this, s);
    }
}

//<summary>Primitive unicode regex value.</summary>
__internal entity PathRegex provides Some {
}

//<summary>Primitive path value.</summary>
__internal __constructable __typedeclable entity Path<T: PathValidator> provides Some, KeyType {
    private function s_into(s: String): Path<T> = special_inject;
    method value(): String = special_extract;
}

/*
Implicitly augments the PathValidator description path component with "./" and "../" where legal.
May be any contigious substring of the that is a contained in the Validator (must end on boundaries of component)
*/
//<summary>Primitive path fragment value.</summary>
__internal __constructable __typedeclable entity PathFragment<T: PathValidator> provides Some, KeyType {
    private function s_into(s: String): PathFragment<T> = special_inject;
    method value(): String = special_extract;

    private function s_isPrefix(): Boolean = pathfragment_isprefix;
    private function s_isSuffix(): Boolean = pathfragment_issuffix;

    method isPrefix(): Boolean {
        return PathFragment<T>::s_isPrefix(this);
    }

    method isSuffix(): Boolean {
        return PathFragment<T>::s_isSuffix(this);
    }
}

//<summary>Primitive safe string value.</summary>
__internal __constructable  __typedeclable entity PathGlob<T: PathValidator> provides Some, KeyType {
    private function s_into(s: String): PathGlob<T> = special_inject;
    method value(): String = special_extract;
}

//<summary>Primitive type for task identifiers</summary>
typedecl TaskID = UUIDv4;

//<summary>Static methods for Tuples.</summary>
__universal concept Tuple provides Some {
}

//<summary>Static methods for Records.</summary>
__universal concept Record provides Some {
}

//<summary>Generic type for all Option<T> to inherit from.</summary>
__universal concept IOption provides Some {
}

//<summary>Generic type for all Something<T> to inherit from.</summary>
__universal concept ISomething provides IOption {
}

//<summary>Type for representing the nothing option value.</summary>
__internal entity Nothing provides IOption {
}

//<summary>Type for representing the nothing option value.</summary>
__internal __constructable entity Something<T> provides ISomething, Option<T> {
    method value(): T = special_extract;
}

concept Option<T> provides IOption {
    method data(): T {
        return this@<Something<T>>.value();
    }

    method convert<V>(): Option<V> {
        if(this === nothing) {
            return nothing;
        }
        else {
            return Something{this.data()@<V>};
        }
    }
}

//<summary>Generic type for all Result<T, E> to inherit from.</summary>
__universal concept IResult provides Some {
}

//<summary>Generic type for all Result<T, E>::Ok to inherit from.</summary>
__universal concept IOk provides Some {
}

//<summary>Generic type for all Result<T, E>::Err to inherit from.</summary>
__universal concept IErr provides Some {
}

//<summary>Result type.</summary>
concept Result<T, E> provides IResult {
    __internal __constructable entity Ok provides IOk, Result<T, E> {
        method getv(): T = special_extract;
    }

    __internal __constructable entity Err provides IErr, Result<T, E> {
        method gete(): E = special_extract;
    }

    method isOk(): Bool {
        return this@<Result<T, E>::Ok>;
    }

    method isErr(): Bool {
        return this@<Result<T, E>::Err>;
    }

    method value(): T {
        return this@<Result<T, E>::Ok>.getv();
    }

    method error(): E {
        return this@<Result<T, E>::Err>.gete();
    }

    method convert<U, V>(): Result<U, V> {
        if(this?<Result<T, E>::Ok>) {
            return Result<U, V>::Ok{this.value()@<U>};
        }
        else {
            return Result<U, V>::Err{this.error()@<V>};
        }
    }
}

//<summary>Result type.</summary>
concept APIResult<T> provides Some {
    //API rejected the input on preconds or validates
    __internal __constructable entity Rejected provides APIResult<T> {
    }

    //API rejected the result as failing a postcondition -- or explicit failure return
    __internal __constructable entity Failed provides APIResult<T> {
    }

    //A hard failure -- unhandled exception
    __internal __constructable entity Error provides APIResult<T> {
    }

    //A value is produced any -- maybe softcheck ensures info along with explicit return
    __internal __constructable entity Success provides Result<T> {
    }
}

//<summary>All actual entities/concepts implicitly provide this concept.</summary>
__universal concept Object provides Some {
}

#if CHECK_LIBS
internal function s_blockingfailure<T>(): T = s_blockingfailure;

internal function s_reshavoc<T>(): T = s_reshavoc;
#endif

//<summary>All collections provide this concept so we can expand them with spread operators.</summary>
__universal concept Expandoable<T> provides Some {
}

__assume_safe internal function s_safeAs<T, U>(v: T): U = special_function;
