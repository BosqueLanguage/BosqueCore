namespace Core;

namespace List {
}

__internal entity List<T> {
#if STRIPPED_CORE
#else
    method empty(): Bool {
        return ListOps::s_list_empty<T>(this);
    }

    method size(): Nat {
        return ListOps::s_list_size<T>(this);
    }

    method lastIndex(): Nat 
        requires !this.empty();
    {
        return ListOps::s_list_size<T>(this) - 1n;
    }

    method isSingleElement(): Bool {
        return ListOps::s_list_size<T>(this) == 1n;
    }

    method single(): T 
        requires this.isSingleElement();
    {
        return ListOps::s_list_front<T>(this);
    }

    method front(): T 
        requires !this.empty();
    {
        return ListOps::s_list_front<T>(this);
    }

    method back(): T 
        requires !this.empty();
    {
        return ListOps::s_list_back<T>(this);
    }

    method get(i: Nat): T 
        requires i < this.size();
    {
        return ListOps::s_list_get<T>(this, i);
    }

    method setFront(v: T): List<T> 
        requires !this.empty();
    {
        return ListOps::s_list_set_front<T>(this, v);
    }

    method setBack(v: T): List<T> 
        requires !this.empty();
    {
        return ListOps::s_list_set_back<T>(this, v);
    }

    method set(i: Nat, v: T): List<T> 
        requires i < this.size();
    {
        return ListOps::s_list_set<T>(this, i, v);
    }

    method pushFront(v: T): List<T> {
        return ListOps::s_list_push_front<T>(this, v);
    }

    method pushBack(v: T): List<T> {
        return ListOps::s_list_push_back<T>(this, v);
    }

    method insert(i: Nat, v: T): List<T> 
        requires i <= this.size();
    {
        return ListOps::s_list_insert<T>(this, i, v);
    }

    method delete(i: Nat): List<T>
        requires i <= this.size();
    {
        return ListOps::s_list_delete<T>(this, i);
    }

    method append(l: List<T>): List<T> {
        return ListOps::s_concat2<T>(this, l);
    }

    method prepend(l: List<T>): List<T> {
        return ListOps::s_concat2<T>(l, this);
    }

    function concat(...ll: List<List<T>>): List<T> {
        if(ll.empty()) {
            return List<T>{};
        }
        else {
            if(ll.isSingleElement()) {
                return ll.single();
            }
            else {
                return ll.reduce<List<T>>(List<T>{}, fn(acc, l) => ListOps::s_concat2<T>(acc, l));
            }
        }
    }

    function concatAll(ll: List<List<T>>): List<T> {
        if(ll.empty()) {
            return List<T>{};
        }
        else {
            if(ll.isSingleElement()) {
                return ll.single();
            }
            else {
                return ll.reduce<List<T>>(List<T>{}, fn(acc, l) => ListOps::s_concat2<T>(acc, l));
            }
        }
    }

    recursive? method allOf(p: recursive? pred(T) -> Bool): Bool {
        if(ListOps::s_list_empty<T>(this)) {
            return true;
        }
        else {
            return ListOps::s_list_allof[recursive?]<T>(this, p);
        }
    }

    recursive? method noneOf(p: recursive? pred(T) -> Bool): Bool {
        if(ListOps::s_list_empty<T>(this)) {
            return true;
        }
        else {
            return ListOps::s_list_noneof[recursive?]<T>(this, p);
        }
    }

    recursive? method someOf(p: recursive? pred(T) -> Bool): Bool {
        if(ListOps::s_list_empty<T>(this)) {
            return false;
        }
        else {
            return ListOps::s_list_someof[recursive?]<T>(this, p);
        }
    }
    
    recursive? method find(p: recursive? pred(T) -> Bool): T {
        return this.tryFind[recursive?](p)@some;
    }

    recursive? method findLast(p: recursive? pred(T) -> Bool): T {
        return this.tryFindLast[recursive?](p)@some;
    }

    recursive? method tryFind(p: recursive? pred(T) -> Bool): Option<T> {
        if(ListOps::s_list_empty<T>(this)) {
            return none;
        }
        else {
            return ListOps::s_list_find[recursive?]<T>(this, p);
        }
    }

    recursive? method tryFindLast(p: recursive? pred(T) -> Bool): Option<T> {
        if(ListOps::s_list_empty<T>(this)) {
            return none;
        }
        else {
            return ListOps::s_list_find_last[recursive?]<T>(this, p);
        }
    }

    method {when T: keytype} contains(v: T): Bool {
        if(ListOps::s_list_empty<T>(this)) {
            return false;
        }
        else {
            return ListOps::s_list_contains<T>(this, v);
        }
    }

    recursive? method map<U>(f: recursive? fn(T) -> U): List<U> {
        if(ListOps::s_list_empty<T>(this)) {
            return List<U>{};
        }
        else {
            return ListOps::s_list_map[recursive?]<T, U>(this, f);
        }
    }

    recursive? method mapIdx<U>(f: recursive? fn(T, Nat) -> U): List<U> {
        if(ListOps::s_list_empty<T>(this)) {
            return List<U>{};
        }
        else {
            return ListOps::s_list_mapidx[recursive?]<T, U>(this, f);
        }
    }

    recursive? method filter(p: recursive? pred(T) -> Bool): List<T> {
        if(ListOps::s_list_empty<T>(this)) {
            return List<T>{};
        }
        else {
            return ListOps::s_list_filter[recursive?]<T>(this, p);
        }
    }

    recursive? method filterIdx(p: recursive? pred(T, Nat) -> Bool): List<T> {
        if(ListOps::s_list_empty<T>(this)) {
            return List<T>{};
        }
        else {
            return ListOps::s_list_filteridx[recursive?]<T>(this, p);
        }
    }

    recursive? method reduce<U>(init: U, f: recursive? fn(U, T) -> U): U {
        if(ListOps::s_list_empty<T>(this)) {
            return init;
        }
        else {
            return ListOps::s_list_reduce[recursive?]<T, U>(this, init, f);
        }
    }

    recursive? method transduce<E, U>(environment: E, f: recursive? fn(E, T) -> (|E, U|)): E, List<U> {
        if(ListOps::s_list_empty<T>(this)) {
            return environment, List<U>{};
        }
        else {
            return ListOps::s_list_transduce[recursive?]<T, E, U>(this, environment, f);
        }
    }

    method {when T: numeric} sum(): T {
        if(ListOps::s_list_empty<T>(this)) {
            return T::zero;
        }
        else {
            return ListOps::s_list_sum<T>(this, T::zero);
        }
    }
#endif %% STRIPPED_CORE 
}
