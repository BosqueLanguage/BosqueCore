%*
 * Take a Bosque IR Assembly and transform it into a SMT representation:
 *%

declare namespace SMTEmitter {
    using BSQAssembly;
    using SMTAssembly;
}

const s_ignoreErrTrgt: Nat = 0n;
const s_invalidErrTrgt: Nat = 65536n;

entity Transformer {
    function transformAssembly(assembly: BSQAssembly::Assembly): SMTAssembly::Assembly {
        let explicitAssembly = BSQAssembly::ExplicitifyTransform::process(assembly);
        let simpleAssembly = BSQAssembly::ConstantSimplification::process(explicitAssembly);

        let treeAssembly = simpleAssembly; %%TODO:: treeflow.bsq
        
        let callg = BSQToSMTCallGraphBuilder::constructBSQToSMTCallGraph(treeAssembly);
        let errinvokes: Map<BSQAssembly::InvokeKey, Bool> = MayErrorAnalysis::generateInvokeErrorInfo(treeAssembly, callg);

        return SMTTransformer::transformAssemblyToSMT(treeAssembly, callg, errinvokes);
    }
}

public function main(mode: CString, assembly: BSQAssembly::Assembly): CString {
    if(mode !== '--errtest' && mode !== '--chktest') {
        let tasm = Transformer::transformAssembly(assembly);
        return SMTEmitter::emitAssembly(tasm, (|'[NO_ERROR_TRGT]', SMTEmitter::s_ignoreErrTrgt|));
    }
    else {
        if(mode === '--chktest') {
            let tasm, opc = Transformer::transformAssemblyAsCheckTest(assembly);
            return CString::concat(SMTEmitter::emitAssembly(tasm, (|'[NO_ERROR_TRGT]', SMTEmitter::s_ignoreErrTrgt|)), '%n;%n;', opc);
        }
        else {
            let tasm, opc = Transformer::transformAssemblyAsErrorTest(assembly);

            let erroropts = tasm.getAllErrors();
            let errcompiles = erroropts.map<CString>(fn(err) => {
                let smtstr = SMTEmitter::emitAssembly(tasm, (|err.ininvoke@some, err.errCtr|));

                let smtchk = CString::concat(';;Error in ', err.ininvoke@some, '%n;%n;', smtstr, '%n;', opc);
                return CString::concat('#### CHECK ####%n;', smtchk);
            });

            return CString::joinAll('%n;%n;', errcompiles);
        }
    }
}
