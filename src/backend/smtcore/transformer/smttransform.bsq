
namespace SMTEmitter;

namespace TransformNameManager {
    const c_dangerous: Map<CString, CString> = Map<CString, CString>{
        'ite' => '@ite'
    };

    function resymbolCStringForSMT(cstr: CString): CString {
        return cstr.replaceAllStringOccurrences('::', '@')
            .replaceAllStringOccurrences(', ', '-')
            .replaceAllStringOccurrences('(|', '@EList<')
            .replaceAllStringOccurrences('|)', '>');
    }

    function safeifyName(name: CString): CString {
        if(!TransformNameManager::c_dangerous.has(name)) {
            return name;
        }
        else {
            return TransformNameManager::c_dangerous.get(name);
        }    
    }

    function convertNamespaceKey(nskey: BSQAssembly::NamespaceKey): SMTAssembly::NamespaceKey {
        return SMTAssembly::NamespaceKey::from(resymbolCStringForSMT(nskey.value));
    }

    function convertTypeKey(nskey: BSQAssembly::TypeKey): SMTAssembly::TypeKey {
        return SMTAssembly::TypeKey::from(resymbolCStringForSMT(nskey.value));
    }

    function convertInvokeKey(ikey: BSQAssembly::InvokeKey): SMTAssembly::InvokeKey {
        return SMTAssembly::InvokeKey::from(resymbolCStringForSMT(ikey.value));
    }
}

datatype ExpressionTransformResult using {
    field preops: List<SMTAssembly::FlowOperation>;
}
of
ValueTransformResult {
    field value: SMTAssembly::Value;
}
| InvokeExpressionTransformResult {
    field iexp: SMTAssembly::InvokeExpression;
}
;

entity SMTTransformer {
    field assembly: BSQAssembly::Assembly;

    method transformStdTypeToSMT(tsig: BSQAssembly::TypeSignature): SMTAssembly::TypeKey {
        if(tsig)@!<BSQAssembly::NominalTypeSignature> {
            let smtk = TransformNameManager::convertTypeKey(tsig.tkeystr);
            return smtk;
        }
        else {
            if(this.assembly.isNominalTypeConcrete($tsig.tkeystr)) {
                return SMTAssembly::TypeKey::from($tsig.tkeystr);
            }
            else {
                return SMTAssembly::TypeKey::from('@BTerm');
            }
        }
    }

    method transformCallTypeToSMT(tsig: BSQAssembly::TypeSignature): SMTAssembly::ParamTypeKey {
        if(tsig)@!<BSQAssembly::LambdaTypeSignature> {
            return SMTAssembly::ParamTypeKey::from(this.transformStdTypeToSMT(tsig));
        }
        else {
            abort; %% NOT IMPLEMENTED LAMBDA TYPES
        }
    }

    method transformLiteralNoneExpression(exp: BSQAssembly::LiteralNoneExpression): SMTAssembly::Value {
        return SMTAssembly::LiteralNoneValue{};
    }

    method transformLiteralSimpleExpression(exp: BSQAssembly::LiteralSimpleExpression): SMTAssembly::Value {
        let tkey = this.transformStdTypeToSMT(exp.etype);
        return SMTAssembly::LiteralSimpleValue{tkey, exp.value};
    }

    method transformLiteralCStringExpression(exp: BSQAssembly::LiteralCStringExpression): SMTAssembly::Value {
        return SMTAssembly::LiteralCStringValue{exp.value};
    }

    method transformLiteralStringExpression(exp: BSQAssembly::LiteralStringExpression): SMTAssembly::Value {
        return SMTAssembly::LiteralStringValue{exp.value};
    }

    method transformExpressionToSMT(exp: BSQAssembly::Expression, ttype: BSQAssembly::TypeSignature): ExpressionTransformResult {
        match(exp)@ {
            BSQAssembly::LiteralNoneExpression => { 
                return ValueTransformResult{List<SMTAssembly::FlowOperation>{}, this.transformLiteralNoneExpression($exp)}; 
            }
            | BSQAssembly::LiteralSimpleExpression => { 
                return ValueTransformResult{List<SMTAssembly::FlowOperation>{}, this.transformLiteralSimpleExpression($exp)}; 
            }
            | BSQAssembly::LiteralCStringExpression => { 
                return ValueTransformResult{List<SMTAssembly::FlowOperation>{}, this.transformLiteralCStringExpression($exp)};
            }
            | BSQAssembly::LiteralStringExpression => { 
                return ValueTransformResult{List<SMTAssembly::FlowOperation>{}, this.transformLiteralStringExpression($exp)}; 
            }
            | _ => { 
                abort; %% NOT IMPLEMENTED
            }
        }
    }

    method transformToplevelExpressionToSMT(exp: BSQAssembly::Expression, ttype: BSQAssembly::TypeSignature): SMTAssembly::OperationBlock {
        let etr = this.transformExpressionToSMT(exp, ttype);

        if(etr)@<ValueTransformResult> {
            return SMTAssembly::OperationBlock{$etr.preops, SMTAssembly::ReturnValueOperation{$etr.value}};
        }
        else {
            abort; %% NOT IMPLEMENTED
        }
    }

    method transformStatementListToSMT(stmts: List<BSQAssembly::Statement>, ttype: BSQAssembly::TypeSignature): SMTAssembly::OperationBlock {
        %% special case for return singletons
        if(stmts.isSingleElement()) {
            let etr = stmts.single();
            match(etr)@ {
                BSQAssembly::ReturnVoidStatement => {
                    abort; %% NOT IMPLEMENTED
                }
                | BSQAssembly::ReturnSingleStatement => {
                    %%TODO: do coerce checking here
                    return this.transformToplevelExpressionToSMT($etr.value, ttype);
                }
                | BSQAssembly::ReturnMultiStatement => {
                    abort; %% NOT IMPLEMENTED
                }
            }
        }
        else {
            abort; %% NOT IMPLEMENTED
        }
    }

    method transformRealBodyImplementationToSMT(impl: BSQAssembly::BodyImplementation, ttype: BSQAssembly::TypeSignature): SMTAssembly::OperationBlock {
        match(impl)@ {
            BSQAssembly::BuiltinBodyImplementation => {
                abort; %% NOT IMPLEMENTED
            }
            | BSQAssembly::ExpressionBodyImplementation => {
                return this.transformToplevelExpressionToSMT($impl.exp, ttype);
            }
            | BSQAssembly::StandardBodyImplementation => {
                return this.transformStatementListToSMT($impl.statements, ttype);
            }
            | _ => {
                abort; %% NOT IMPLEMENTED
            }
        }
    }

    method transformNamespaceFunctionDeclToSMT(decl: BSQAssembly::NamespaceFunctionDecl, errinvokes: Map<BSQAssembly::InvokeKey, Bool>): SMTAssembly::NamespaceFunctionDecl {
        let nskey = TransformNameManager::convertNamespaceKey(decl.declaredInNS);
        let ikey = TransformNameManager::convertInvokeKey(decl.ikey);

        let params = decl.params.map<SMTAssembly::InvokeParameterDecl>(fn(p) => SMTAssembly::InvokeParameterDecl{p.pname.value, this.transformCallTypeToSMT(p.ptype)});
        let rtype = this.transformStdTypeToSMT(decl.resultType);
        let artype = if(errinvokes.get(decl.ikey)) then SMTAssembly::OptionResult{ rtype } else SMTAssembly::DirectResult{ rtype };

        let body = this.transformToplevelExpressionToSMT(decl.body, decl.resultType);
        return SMTAssembly::NamespaceFunctionDecl{ nskey, decl.name.value, ikey, params, artype, body };
    }

    method transformNamspaceConstDeclToSMT(decl: BSQAssembly::NamespaceConstDecl): SMTAssembly::NamespaceConstDecl {
        let nskey = TransformNameManager::convertNamespaceKey(decl.declaredInNS);

        let declaredtype = this.transformStdTypeToSMT(decl.declaredType);
        let vtype = SMTAssembly::OptionResult{ declaredtype };

        let body = this.transformToplevelExpressionToSMT(decl.value, decl.declaredType);
        return SMTAssembly::NamespaceConstDecl{ nskey, decl.name.value, vtype, body };
    }

    function transformAssemblyToSMT(assembly: BSQAssembly::Assembly, errinvokes: Map<BSQAssembly::InvokeKey, Bool>): SMTAssembly::Assembly {
        let transformer = SMTTransformer{assembly};

        let tnsfuncs = assembly.allfuncs
            .filter(pred(ikey) => assembly.nsfuncs.has(ikey))
            .reduce<Map<SMTAssembly::InvokeKey, SMTAssembly::NamespaceFunctionDecl>>(Map<SMTAssembly::InvokeKey, SMTAssembly::NamespaceFunctionDecl>>{}, 
                fn(acc, ikey) => {
                    let bsqdecl = assembly.nsfuncs.get(ikey);
                    let smtdecl = transformer.transformNamespaceFunctionDeclToSMT(bsqdecl, errinvokes);
                    return acc.set(smtdecl.ikey, smtdecl);
                });

        let tallfuncs = assembly.allfuncs.map(fn(ikey) => TransformNameManager::convertInvokeKey(ikey));
        %% TODO -- topo sort this

        return SMTAssembly::Assembly {
            nsconsts = assembly.nsconsts.map<SMTAssembly::NamespaceConstDecl>(fn(nsc) => transformer.transformNamspaceConstDeclToSMT(nsc)),
            typeconsts = List<SMTAssembly::ConstMemberDecl>{},

            nspreds = List<SMTAssembly::NamespacePredicateFunctionDecl>{},
            nsbuiltins = List<SMTAssembly::NamespaceBuiltinFunctionDecl>{},
    
            nsfuncs = tnsfuncs,
            typefuncs = Map<SMTAssembly::InvokeKey, SMTAssembly::TypeFunctionDecl>{},
            allfuncs = tallfuncs,
    
            enums = List<SMTAssembly::EnumTypeDecl>{},
            typedecls = List<SMTAssembly::TypedeclTypeDecl>{},
            constructables = List<SMTAssembly::ConstructableTypeDecl>{},
            collections = List<SMTAssembly::CollectionTypeDecl>{},

            entities = List<SMTAssembly::EntityTypeDecl>{},
            datamembers = List<SMTAssembly::DatatypeMemberEntityTypeDecl>{},

            pconcepts = List<SMTAssembly::PrimitiveConceptTypeDecl>{},
            concepts = List<SMTAssembly::ConceptTypeDecl>{},

            datatypes = List<SMTAssembly::DatatypeTypeDecl>{}
        };
    }
}
