namespace CPPEmitter;

%%
%% TODO: We will need to properly overload our conversion from bosque types to cpp types to ensure
%% everything stays safe (such as Nat in bosque only being 63 bits not 64) and all operations on 
%% these values need to be provided via overloading and using compiler builtins (like __builtin_add_overflow__)
%%

%% CPP Pre-defined backend
namespace PathStack {
    function emitPathStackCreate(): CString {
        return 'PathStack ps = PathStack::create();';
    }

    function emitPathStackLeft(): CString {
        return 'ps.left();';
    }

    function emitPathStackRight(): CString {
        return 'ps.right();';
    }

    function emitPathStackUp(): CString {
        return 'ps.up();';
    }
}

namespace CCharBuffer {
    %% Emit c++ for buffer creation here
}

namespace UnicodeCharBuffer {
    %% Emit c++ for buffer creation here
}

function emitTypeSignature(ts: CPPAssembly::TypeSignature): CString {
    return ts.tkeystr.value;
}

function emitIdentifier(ident: CPPAssembly::Identifier): CString {
    return ident.value;
}

function emitVarIdentifier(vident: CPPAssembly::VarIdentifier): CString {
    return vident.value;
}

function emitFunction(ik: CPPAssembly::InvokeKey): CString {
    return ik.value;
}

function emitNamespaceKey(nsk: CPPAssembly::NamespaceKey): CString {
    return nsk.value;
}

function emitLiteralSimpleExpression(exp: CPPAssembly::LiteralSimpleExpression): CString {
    %% We may want to make this matching a tad nicer, although not too bad as is 
    switch(exp.etype.tkeystr.value) {
        '__CoreCpp::Int' => { return CString::concat(exp.value, '_i'); }
        | '__CoreCpp::BigInt' => { return CString::concat(exp.value, '_I'); } 
        | '__CoreCpp::Nat' => { return CString::concat(exp.value, '_n'); }
        | '__CoreCpp::BigNat' => { return CString::concat(exp.value, '_N'); }
        | '__CoreCpp::Float' => { return CString::concat(exp.value, '_f'); }
        | 'bool' => { return exp.value; }
        | _ => { abort; }
    }
}

function emitAccessVariableExpression(exp: CPPAssembly::AccessVariableExpression): CString {
    %% May need some work with type
    return emitVarIdentifier(exp.vname);
}

function emitReturnSingleStatement(ret: CPPAssembly::ReturnSingleStatement, indent: CString): CString {
    %% let rtype = emitTypeSignature(ret.rtype);
    let exp = emitExpression(ret.value);

    let full_indent: CString = CString::concat(indent, '    ');
    return CString::concat(full_indent, 'return ', exp, ';%n;');
}

recursive function emitBinAddExpression(add: CPPAssembly::BinAddExpression): CString {
    let lhs = emitExpression[recursive](add.lhs);
    let rhs = emitExpression[recursive](add.rhs);

    return CString::concat('(', lhs, ' + ', rhs, ')');
}

recursive function emitBinSubExpression(sub: CPPAssembly::BinSubExpression): CString {
    let lhs = emitExpression[recursive](sub.lhs);
    let rhs = emitExpression[recursive](sub.rhs);

    return CString::concat('(', lhs, ' - ', rhs, ')');
}

recursive function emitBinDivExpression(div: CPPAssembly::BinDivExpression): CString {
    let lhs = emitExpression[recursive](div.lhs);
    let rhs = emitExpression[recursive](div.rhs);

    return CString::concat('(', lhs, ' / ', rhs, ')');
}

recursive function emitBinMultExpression(mult: CPPAssembly::BinMultExpression): CString {
    let lhs = emitExpression[recursive](mult.lhs);
    let rhs = emitExpression[recursive](mult.rhs);

    return CString::concat('(', lhs, ' * ', rhs, ')');
}

recursive function emitBinaryArithExpression(e: CPPAssembly::BinaryArithExpression): CString {
    match(e)@ {
        CPPAssembly::BinAddExpression => { return emitBinAddExpression[recursive]($e); }
        | CPPAssembly::BinSubExpression => { return emitBinSubExpression[recursive]($e); }
        | CPPAssembly::BinDivExpression => { return emitBinDivExpression[recursive]($e); }
        | CPPAssembly::BinMultExpression => { return emitBinMultExpression[recursive]($e); }
    }
}

recursive function emitPrefixNotOpExpression(notop: CPPAssembly::PrefixNotOpExpression): CString {
    let expr = emitExpression[recursive](notop.expr);
    return CString::concat('(', '!', expr, ')');
}

recursive function emitPrefixNegateOpExpression(negop: CPPAssembly::PrefixNegateOpExpression): CString {
    let expr = emitExpression[recursive](negop.expr);
    return CString::concat('(', '-', expr, ')');
}

%% I think this is how prefix plus works? doesnt convert value to positive, just visual specifier?
recursive function emitPrefixPlusOpExpression(plusop: CPPAssembly::PrefixPlusOpExpression): CString {
    let expr = emitExpression[recursive](plusop.expr);
    return CString::concat('(', expr, ')');
}

recursive function emitUnaryExpression(e: CPPAssembly::UnaryExpression): CString {
    match(e)@ {
        CPPAssembly::PrefixNotOpExpression => { return emitPrefixNotOpExpression[recursive]($e); }
        | CPPAssembly::PrefixNegateOpExpression => { return emitPrefixNegateOpExpression[recursive]($e); }
        | CPPAssembly::PrefixPlusOpExpression => { return emitPrefixPlusOpExpression[recursive]($e); }
    }
}

recursive function emitNumericEqExpression(e: CPPAssembly::NumericEqExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' == ', rhs, ')');   
}

recursive function emitNumericNeqExpression(e: CPPAssembly::NumericNeqExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' != ', rhs, ')');   
}

recursive function emitNumericLessExpression(e: CPPAssembly::NumericLessExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' < ', rhs, ')');   
}

recursive function emitNumericLessEqExpression(e: CPPAssembly::NumericLessEqExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' <= ', rhs, ')');   
}

recursive function emitNumericGreaterExpression(e: CPPAssembly::NumericGreaterExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' > ', rhs, ')');   
}

recursive function emitNumericGreaterEqExpression(e: CPPAssembly::NumericGreaterEqExpression): CString {
    let lhs = emitExpression[recursive](e.lhs);
    let rhs = emitExpression[recursive](e.rhs);

    return CString::concat('(', lhs, ' >= ', rhs, ')');   
}

recursive function emitBinaryNumericExpression(e: CPPAssembly::BinaryNumericExpression): CString {
    match(e)@ {
        CPPAssembly::NumericEqExpression => { return emitNumericEqExpression[recursive]($e); }
        | CPPAssembly::NumericNeqExpression => { return emitNumericNeqExpression[recursive]($e); }
        | CPPAssembly::NumericLessExpression => { return emitNumericLessExpression[recursive]($e); }
        | CPPAssembly::NumericLessEqExpression => { return emitNumericLessEqExpression[recursive]($e); }
        | CPPAssembly::NumericGreaterExpression => { return emitNumericGreaterExpression[recursive]($e); }
        | CPPAssembly::NumericGreaterEqExpression => { return emitNumericGreaterEqExpression[recursive]($e); }
    }
}

function emitExpression(e: CPPAssembly::Expression): CString {
    match(e)@ {
        CPPAssembly::BinaryArithExpression => { return emitBinaryArithExpression[recursive]($e); }
        | CPPAssembly::BinaryNumericExpression => { return emitBinaryNumericExpression[recursive]($e); }
        | CPPAssembly::UnaryExpression => { return emitUnaryExpression[recursive]($e); }
        | CPPAssembly::LiteralSimpleExpression => { return emitLiteralSimpleExpression($e); }
        | CPPAssembly::AccessVariableExpression => { return emitAccessVariableExpression($e); }
        | _ => { abort; }
    }
}

function emitVariableInitializationStatement(stmt: CPPAssembly::VariableInitializationStatement, indent: CString): CString {
    let name = emitIdentifier(stmt.name);
    let stype = emitTypeSignature(stmt.vtype);
    let exp = emitExpression(stmt.exp);

    let full_indent: CString = CString::concat(indent, '    ', stype); %% List constructor size max 6
    return CString::concat(full_indent, ' ', name, ' = ', exp, ';');
}

function emitStatement(stmt: CPPAssembly::Statement, indent: CString): CString {
    match(stmt)@ {
        CPPAssembly::VariableInitializationStatement => { return emitVariableInitializationStatement($stmt, indent); }
        | CPPAssembly::ReturnSingleStatement => { return emitReturnSingleStatement($stmt, indent); }
        | _ => { abort; }
    }
}

function emitStandardBodyImplementation(body: CPPAssembly::StandardBodyImplementation, indent: CString): CString {
    return CString::joinAll('%n;', body.statements.map<CString>(fn(stmt) => emitStatement(stmt, indent)));
}

function emitBodyImplementation(body: CPPAssembly::BodyImplementation, indent: CString): CString {
    match(body)@ {
        %% CPPAssembly::AbstractBodyImplementation => { abort; }
        %% | CPPAssembly::PredicateUFBodyImplementation => { abort; }
        %% | CPPAssembly::BuiltinBodyImplementation => { abort; }
        %% | CPPAssembly::SynthesisBodyImplementation => { abort; }
        %% | CPPAssembly::ExpressionBodyImplementation => { abort; }
        CPPAssembly::StandardBodyImplementation => { return emitStandardBodyImplementation($body, indent); }
        | _ => { abort; }
    }
}

%% Will need to specific namespace of function
function emitNamespaceFunctionDecl(func: CPPAssembly::NamespaceFunctionDecl, indent: CString): CString {
    let name = func.name;
    let nskey = emitNamespaceKey(func.ns);
    let params = ''; %% TODO: Parameters not implemented
    let rtype = emitTypeSignature(func.resultType); 

    let pre: CString = CString::concat(rtype, ' ', name );
    let params_impl: CString = CString::concat('(', params, ')');

    return CString::concat(pre, params_impl, ' {%n;', emitBodyImplementation(func.body, indent), indent, '}%n;');
}

%% Emits all funcions inside a given namespace
function emitNamespaceDecl(nsdecl: CPPAssembly::NamespaceKey, funcs: CString): CString {
    return CString::concat('namespace ', nsdecl.value, ' {%n;', funcs, '}%n;%n;');
}

function emitAssembly(asm: CPPAssembly::Assembly): CString {
    %% First emit all namespace blocks with their functions
    let nsblocks = asm.nsfuncs.reduce<CString>('', fn(acc, nskey, funcs) => {
        let emission = funcs.reduce<CString>('', fn(funcacc, ikey, func) => {
            let indent: CString = '    ';
            return CString::concat(indent, funcacc, emitNamespaceFunctionDecl(func, indent));
        });
        
        return emitNamespaceDecl(nskey, emission);
    });

    %% TODO: Other non namespace functions

    %% For CCharBuf and Unicode... will need to emit builtin functions explicitly

    return CString::join('%n;', nsblocks);
}