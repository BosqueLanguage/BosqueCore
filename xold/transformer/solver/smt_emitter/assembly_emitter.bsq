namespace SMTEmit;

import TreeIR;

function typeEncodedAsUnion(tt: TreeIR::TypeKey): Bool {
    let ttype = this.assembly.typeMap.get(tt);
    return (ttype?<TreeIR::ConceptType> || ttype?<TreeIR::UnionType>);
}

entity TypeInfoEmitter {
    field assembly: TreeIR::Assembly;
    field nemitter: NameEmitter;

    method getSMTTypeName(t: TreeIR::Type | TreeIR::TypeKey): String {
        let ttype = if <TreeIR::Type> (t) then $ else this.assembly.typeMap.get($);
        return this.nemitter.getSMTTypeName(ttype);
    }

    recursive method isSubtypeOf(t: TreeIR::Type, oftype: TreeIR::AbstractType): Bool {
        if(t.tkey === oftype.tkey) {
            return true;
        }
        elif(t.supertypes?none) {
            return false;
        }
        else {
            return t.supertypes@!none.someOf(pred(st) => this.isSubtypeOf[recursive](this.assembly.typeMap.get(st), oftype));
        }
    }

    method emitKeyTypeTagDecl(t: TreeIR::Type): String? {
        if(!t?<OOType> || t?<TreeIR::PrimitiveInternalEntityType>) {
            return none;
        }

        if(t@<OOType>.iskeytype) {
            return this.nemitter.getKeyTagName(t.tkey);
        }
        else {
            return none;
        }
    }

    function typeRequiresConstructorInfo(t: TreeIR::Type): Bool {
        match(t) {
            TreeIR::ObjectEntityType => return true;
            | TreeIR::EnumEntityType => return true;
            | TreeIR::MapEntryEntityType => return true;
            | TreeIR::TupleType => return true;
            | TreeIR::RecordType => return true;
            | TreeIR::EListType => return true;
            | _ => return false;
        }
    }

    method generateDeclAndConsInfo(t: TreeIR::Type): {decl: String, cons: String}? {
        if(!TypeInfoEmitter::typeRequiresConstructorInfo(t)) {
            return none;
        }

        let smtname = this.getSMTTypeName(t);
        let smttype = smtifyTypeKey(t.tkey);

        let tdecl = String::concat(List<String>{"(", smttype, " 0)"});

        var tcons: String;
        match(t) {
            TreeIR::ObjectEntityType => {
                let consops = $.allfields.map<String>(fn(f) => String::concat(List<String>{"(", this.nemitter.getFieldAccessorName($.tkey, f.fkey), " ", this.getSMTTypeName(f.ftype), ")"}));
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName($.tkey), " ", String::join(" ", consops), ") )"});
            }
            | TreeIR::EnumEntityType => {
                let consop = String::concat(List<String>{"(", this.nemitter.getEnumValueAccessorName($.tkey), " Int)"});
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName($.tkey), " ", consop, ")) )"});
            }
            | TreeIR::MapEntryEntityType => {
                let consops = List<String>{String::concat(List<String>{"(", this.nemitter.getMapEntryKeyAccessorName($.tkey), " ", this.getSMTTypeName($.typeK), ")"}), String::concat(List<String>{"(", this.nemitter.getMapEntryValueAccessorName($.tkey), " ", this.getSMTTypeName($.typeV), ")"})};
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName(t.tkey), " ", String::join(" ", consops), ") )"});
            }
            | TreeIR::TupleType => {
                let consops = $.types.mapIdx<String>(fn(te, i) => String::concat(List<String>{"(", this.nemitter.getIndexAccessorName($.tkey, i), " ", this.getSMTTypeName(te), ")"}));
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName($.tkey), " ", String::join(" ", consops), ") )"});
            }
            | TreeIR::RecordType => {
                let consops = $.entries.map<String>(fn(re) => String::concat(List<String>{"(", this.nemitter.getPropertyAccessorName($.tkey, re.pname), " ", this.getSMTTypeName(re.ptype), ")"}));
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName($.tkey), " ", String::join(" ", consops), ") )"});
            }
            | TreeIR::EListType => {
                let consops = $.types.mapIdx<String>(fn(ee, i) => String::concat(List<String>{"(", this.nemitter.getIndexAccessorName($.tkey, i), " ", this.getSMTTypeName(ee), ")"}));
                tcons = String::concat(List<String>{"( (", this.nemitter.getConstructorName($.tkey), " ", String::join(" ", consops), ") )"});
            }
        }

        return {decl=tdecl, cons=tcons};
    }

    function typeRequiresBoxingInfo(t: TreeIR::Type): Bool {
        match(t) {
            TreeIR::PrimitiveInternalEntityType => return false;
            | TreeIR::TaskType => return false;
            | TreeIR::ConceptType => return false;
            | TreeIR::ConceptSetType => return false;
            | TreeIR::UnionType => return false;
            | TreeIR::EListType => return false;
            | _ => return true;
        }
    }

    method generateBoxAndUnboxInfo(t: TreeIR::Type): {box: String, unbox: String, typebox: String}? {
        if(!TypeInfoEmitter::typeRequiresBoxingInfo(t)) {
            return none;
        }

        let smtname = this.getSMTTypeName(t);
        let smttype = smtifyTypeKey(t.tkey);

        let btname = this.nemitter.getBoxDataMKName(t.tkey);
        let btfname = this.nemitter.getBoxDataMKNameField(t.tkey);
        let typebox = String::concat(List<String>{"(", btname, " (", btfname, " ", smtname, "))"});

        let bname = this.nemitter.getBoxName(t.tkey);
        let ubname = this.nemitter.getUnboxName(t.tkey);

        var box: String;
        if (!t?<OOType> || !t@<OOType>.iskeytype) {
            box = String::concat(List<String>{"(define-fun ", bname, " ((v ", smtname, ")) @Term (@Term-mk (", btname, " v) @BoxedKey-mk-NA))"});
        }
        else {
            let bkk = String::concat(List<String>{"(@BoxedKey-mk-of @KeyTypeTag-", smtname, " (@BoxedKeyValue-mk-", smtname, " v))"});
            box = String::concat(List<String>{"(define-fun ", bname, " ((v ", smtname, ")) @Term (@Term-mk (", btname, " v) ", bkk, "))"});
        }

        let unbox = String::concat(List<String>{"(define-fun ", ubname, " ((t @Term)) ", smtname, " (",  btfname, " (@Term-data t)))"});

        return {box=box, unbox=unbox, typebox=typebox};
    }

    function typeRequiresResultInfo(t: TreeIR::Type): Bool {
        match(t) {
            TreeIR::PrimitiveInternalEntityType => return false;
            | TreeIR::TaskType => return false;
            | TreeIR::EListType => return false;
            | _ => return true;
        }
    }

    method generateResultInfo(t: TreeIR::Type): {odecl: String, ocons: String, tdecl: String, tcons: String}? {
        if(!TypeInfoEmitter::typeRequiresResultInfo(t)) {
            return none;
        }

        let smtname = this.getSMTTypeName(t);
        let smttype = smtifyTypeKey(t.tkey);

        let odecl = String::concat(List<String>{"(@ResultO-", smttype, " 0)"});
        let ocons = String::concat(List<String>{"( (@ResultO-mk-ok-", smttype, " ", "(@ResultO-", smttype, "-val ", smtname, ")) (@ResultO-mk-err-", smttype, " ", "(@ResultO-", smttype, "-err @ErrorKind)) )"});

        let tdecl = String::concat(List<String>{"(@ResultT-", smttype, " 0)"});
        let tcons = String::concat(List<String>{"( (@ResultT-mk-ok-", smttype, " ", "(@ResultT-", smttype, "-val ", smtname, ")) (@ResultT-mk-err-", smttype, " ", "(@ResultT-", smttype, "-err @ErrorKind)) )"});

        return {odecl=odecl, ocons=ocons, tdecl=tdecl, tcons=tcons};
    }

    recursive method typeRequiresSubtypeInfo(t: TreeIR::Type): Bool {
        if !<TreeIR::AbstractType> (t) {
            return false;
        }

        match(t) {
            TreeIR::ConceptType => return !$.attributes.contains("__universal");
            | TreeIR::ConceptSetType => return $.conceptTypes.allOf(pred(ct) => this.typeRequiresSubtypeInfo[recursive](this.assembly.typeMap.get(ct)));
            | TreeIR::UnionType => return $.options.allOf(pred(tt) => this.typeRequiresSubtypeInfo[recursive](this.assembly.typeMap.get(tt)));
        }
    }

    method generateSubtypeInfo(t: TreeIR::Type): String? {
        if(!this.typeRequiresSubtypeInfo(t)) {
            return none;
        }

        let smtname = this.getSMTTypeName(t);
        let smttype = smtifyTypeKey(t.tkey);
        let csubtypes = this.assembly.typeMap.values().filter(pred(st) => !st?<TreeIR::AbstractType> && this.isSubtypeOf[recursive](st, t@<TreeIR::AbstractType>));

        if(csubtypes.empty()) {
            return none;
        }
        else {
            let subtfn = this.nemitter.getSubtypeOfName(t.tkey);
            let stags = if (csubtypes.size() === 1n) 
                then 
                    String::concat(List<String>{"((_ is ", this.nemitter.getBoxDataMKName(csubtypes.front().tkey), ") tt)"})
                else
                    String::concat(List<String>{"(or ", String::join(" ", csubtypes.map<String>(fn(st) => String::concat(List<String>{"((_ is ", this.nemitter.getBoxDataMKName(st.tkey), ") tt)"}))), ")"});
                ;

            return String::concat(List<String>{"(define-fun ", subtfn, " ((t @Term)) Bool (let (tt (@Term-data t)) ", stags, "))"});
        }
    }
}

entity AssemblyEmitter {
    field assembly: TreeIR::Assembly;
    field nemitter: NameEmitter;
    field temitter: TypeInfoEmitter;

    function processAssembly(asm: TreeIR::Assembly): TreeIR::Assembly {
        //TODO: ensure bodies are trees
        //TODO: make all variable assignments unique
        //TODO: flatten expressions -- error inducing expressions and calls are at top-level

        return asm;
    }

    method generateKeyTypeTagInfo(): {key_type_tag_decls: String, key_type_tag_distincts: String, key_type_tag_sort: String} {
        let alltypes = this.assembly.typeMap.values();
        let ktags = alltypes.map<String?>(fn(t) => this.temitter.emitKeyTypeTagDecl(t)).filterSome();

        let kdecls = String::join("\n", ktags.map<String>(fn(kt) => String::concat(List<String>{"(declare-const ", kt, " @KeyTypeTag)"})));
        let kdistincts = String::join(" ", ktags);
        let ksort = String::join("\n", ktags.mapIdx<String>(fn(kt, ii) => {
            let ltt = if (ii == 0n) then "@KeyTypeTag-SHAContentHash" else ktags.get(ii - 1n);
            return String::concat(List<String>{"(assert (@key_type_sort_order ", ltt, " ", kt, "))"});
        }));

        return {key_type_tag_decls=kdecls, key_type_tag_distincts=kdistincts, key_type_tag_sort=ksort};
    }

    method generateDeclAndConsInfo(): {decls: String, cons: String} {
        let alltypes = this.assembly.typeMap.values();

        let ccinfo = alltypes.map<{decl: String, cons: String}?>(fn(t) => this.temitter.generateDeclAndConsInfo(t)).filterSome();
        let ddecls = String::join("\n    ", ccinfo.map<String>(fn(vv) => vv.decl));
        let dcons = String::join("\n        ", ccinfo.map<String>(fn(vv) => vv.cons));

        return {decls=ddecls, cons=dcons};
    }

    method generateBoxAndUnboxInfo(): {box: String, unbox: String, typebox: String} {
        let alltypes = this.assembly.typeMap.values();

        let bbinfo = alltypes.map<{box: String, unbox: String, typebox: String}?>(fn(t) => this.temitter.generateBoxAndUnboxInfo(t)).filterSome();
        let tbox = String::join("\n        ", bbinfo.map<String>(fn(vv) => vv.typebox));
        let bbox = String::join("\n", bbinfo.map<String>(fn(vv) => vv.box));
        let ubbox = String::join("\n", bbinfo.map<String>(fn(vv) => vv.unbox));

        return {box=bbox, unbox=ubbox, typebox=tbox};
    }

    method generateResultInfo(): {odecls: String, ocons: String, tdecls: String, tcons: String} {
        let alltypes = this.assembly.typeMap.values();

        let bbinfo = alltypes.map<{odecl: String, ocons: String, tdecl: String, tcons: String}?>(fn(t) => this.temitter.generateResultInfo(t)).filterSome();
        let odecls = String::join("\n    ", bbinfo.map<String>(fn(vv) => vv.odecl));
        let ocons = String::join("\n        ", bbinfo.map<String>(fn(vv) => vv.ocons));
        let tdecls = String::join("\n    ", bbinfo.map<String>(fn(vv) => vv.tdecl));
        let tcons = String::join("\n        ", bbinfo.map<String>(fn(vv) => vv.tcons));

        return {odecls=odecls, ocons=ocons, tdecls=tdecls, tcons=tcons};
    }

    method generateSubtypeInfo(): String {
        let alltypes = this.assembly.typeMap.values();

        let stinfo = alltypes.map<String?>(fn(t) => this.temitter.generateSubtypeInfo(t)).filterSome();
        return String::join("\n", stinfo);
    }

    method generateGlobalDeclsForEntityOrConceptList(el: List<TypeKey>): String? {
        let dtypes = el.project<Type>(this.assembly.typeMap).castType<OOType>().filter(pred(t) => !t.constMembers.empty());
        if(dtypes.empty()) {
            return none;
        }

        let ddecls = dtypes.map<String>(fn(t) => {
            let tdecls = t.constMembers.map<String>(fn(cm) => {
                let cname = this.nemitter.getOOMemberConstantName(t.tkey, cm.name);
                let ctype = this.temitter.getSMTTypeName(cm.declaredType);
                return String::concat(List<String>{"(declare-const ", cname, " ", ctype, ")"});
            });
            return String::join(" ", tdecls);
        });

        return String::join("\n", ddecls);
    }

    method generateGlobalDecls(): String {
        let allns = this.assembly.namespaceMap.values();

        let nscdecls = allns.map<String>(fn(ns) => {
            let ndecls = ns.consts.values().map<String>(fn(nsc) => {
                let cname = this.nemitter.getNamespaceConstantName(nsc.ns, nsc.name);
                let ctype = this.temitter.getSMTTypeName(nsc.declaredType);
                return String::concat(List<String>{ "(declare-const ", cname, " ", ctype, ")" });
            });

            let edecls = ns.objects.values().map<String?>(fn(nsel) => this.generateGlobalDeclsForEntityOrConceptList(nsel)).filterSome();
            let cdecls = ns.concepts.values().map<String?>(fn(nscl) => this.generateGlobalDeclsForEntityOrConceptList(nscl)).filterSome();

            return String::join("\n", ListOp::concat<String>(List<List<String>>{List<String>{ String::join(" ", ndecls) }, edecls, cdecls}));
        });

        return String::join("\n\n", nscdecls);
    }

    method generateGlobalDefsForEntityOrConceptList(el: List<TypeKey>, invokeResultErrMap: Map<TreeIR::InvokeKey, TreeIR::ErrorKind>): String? {
        let dtypes = el.project<Type>(this.assembly.typeMap).castType<OOType>().filter(pred(t) => !t.constMembers.empty());
        if(dtypes.empty()) {
            return none;
        }

        let ddefs = dtypes.map<String>(fn(t) => {
            let tdecls = t.constMembers.map<String>(fn(cm) => {
                let cname = this.nemitter.getOOMemberConstantName(t.tkey, cm.name);

                if <TreeIR::Expression>(cm.value) {
                    let bemit = BodyEmitter{this.assembly, this.nemitter, this.temitter, cm.srcFile, -1i, invokeResultErrMap, cm.declaredType, TreeIR::ErrorKind::safe};
                    let cval = bemit.emitExpression($);

                    return String::concat(List<String>{"(assert (= ", cname, " ", cval, "))"});
                }
                else {
                    let bemit = BodyEmitter{this.assembly, this.nemitter, this.temitter, cm.srcFile, -1i, invokeResultErrMap, cm.declaredType, TreeIR::ErrorKind::any};
                    let cval = bemit.emitBodyStatementList($.ops);

                    return String::concat(List<String>{
                        "(assert ", BodyEmitter::buildOkIsTest(cname, cm.declaredType, TreeIR::ErrorKind::any), ")",
                        "(assert (= ", cname, " ", BodyEmitter::buildValueExtract(cval, cm.declaredType, TreeIR::ErrorKind::any), "))"
                    });
                }
            });
            return String::join(" ", tdecls);
        });

        return String::join("\n", ddefs);
    }

    method generateGlobalDefs(invokeResultErrMap: Map<TreeIR::InvokeKey, TreeIR::ErrorKind>): String {
        let allns = this.assembly.namespaceMap.values();

        let nscdecls = allns.map<String>(fn(ns) => {
            let ndecls = ns.consts.values().map<String>(fn(nsc) => {
                let cname = this.nemitter.getNamespaceConstantName(nsc.ns, nsc.name);

                if <TreeIR::Expression>(nsc.value) {
                    let bemit = BodyEmitter{this.assembly, this.nemitter, this.temitter, nsc.srcFile, -1i, invokeResultErrMap, nsc.declaredType, TreeIR::ErrorKind::safe};
                    let cval = bemit.emitExpression($);

                    return String::concat(List<String>{"(assert (= ", cname, " ", cval, "))"});
                }
                else {
                    let bemit = BodyEmitter{this.assembly, this.nemitter, this.temitter, nsc.srcFile, -1i, invokeResultErrMap, nsc.declaredType, TreeIR::ErrorKind::any};
                    let cval = bemit.emitBodyStatementList($.ops);

                    return String::concat(List<String>{
                        "(assert ", BodyEmitter::buildOkIsTest(cname, nsc.declaredType, TreeIR::ErrorKind::any), ")",
                        "(assert (= ", cname, " ", BodyEmitter::buildValueExtract(cval, nsc.declaredType, TreeIR::ErrorKind::any), "))"
                    });
                }
            });

            let edecls = ns.objects.values().map<String?>(fn(nsel) => this.generateGlobalDefsForEntityOrConceptList(nsel, invokeResultErrMap)).filterSome();
            let cdecls = ns.concepts.values().map<String?>(fn(nscl) => this.generateGlobalDefsForEntityOrConceptList(nscl, invokeResultErrMap)).filterSome();

            return String::join("\n", ListOp::concat<String>(List<List<String>>{List<String>{ String::join(" ", ndecls) }, edecls, cdecls}));
        });

        return String::join("\n\n", nscdecls);
    }

    method generateSMTCode(invokeResultErrMap: Map<TreeIR::InvokeKey, TreeIR::ErrorKind>): String {
        let idecls = this.assembly.invokeMap.mapFlat<String>(fn(k, i) => {
            let iname = smtifyInvokeKey(i.invkey);
            let rtype = i.resultType;
            let rerr = if(invokeResultErrMap.has(i.invkey)) then invokeResultErrMap.get(i.invkey) else TreeIR::ErrorKind::safe;
            let bemitter = BodyEmitter{this.assembly, this.nemitter, this.temitter, i.srcFile, -1i, invokeResultErrMap, rtype, rerr};
            
            let args = i.params.map<String>(fn(p) => {
                let pname = p.name.value().value();
                let ptype = this.temitter.getSMTTypeName(p.type);

                return String::concat(List<String>{"(", pname, " ", ptype, ")"});
            });

            let body = match (i) {
                TreeIR::InvokeImplementation => bemitter.emitBodyStatementList($.body)
                | TreeIR::InvokePrimitive => "PRIMITIVE DECL"
                | _ => ""
            };

            if(body === "") {
                return "";
            }
            else {
                let smtrtype = this.temitter.getSMTTypeName(rtype);
                return String::concat(List<String>{"(define-fun ", iname, " (", String::join(" ", args), ") ", smtrtype, "\n", body, "\n)"});
            }
        });

        return String::join("\n", idecls);
    }
}
